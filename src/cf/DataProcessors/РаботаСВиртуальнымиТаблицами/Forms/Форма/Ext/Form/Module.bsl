&НаКлиенте
Процедура КонецПериодаПриИзменении(Элемент)
	
	КонецПериода = КонецДня(КонецПериода);

КонецПроцедуры

&НаКлиенте
Процедура ВыручкаОтПокупателей(Команда)
	
	ПолучитьВыручкуОтПокупателей();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыручкаОтПокупателей2(Команда)
	
	ПолучитьВыручкуОтПокупателей2();
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьВыручкуОтПокупателей()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|		ОборотыДтКт.СубконтоКт1 КАК Покупатель,
	|		ОборотыДтКт.СуммаОборот КАК Выручка
	|ИЗ
	|	РегистрБухгалтерии.ОсновнойРегистрБухгалтерии.ОборотыДтКт(
	|											&НачалоПериода, 
	|											&КонецПериода, 
	|											, 
	|											СчетДт = &СчетКассы, 
	|											, 
	|											СчетКт = &СчетПокупателей, 
	|											, 
	|											) КАК ОборотыДтКт
	|ИТОГИ
	|	СУММА(Выручка)
	|ПО
	|	ОБЩИЕ";
		
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	Запрос.УстановитьПараметр("СчетКассы", ПланыСчетов.ОсновнойПланСчетов.Касса);
	Запрос.УстановитьПараметр("СчетПокупателей", ПланыСчетов.ОсновнойПланСчетов.Покупатели);
	
	Результат = Запрос.Выполнить();
   	Остатки = Результат.Выбрать();
	
	ТЗ.Очистить();
	Пока Остатки.Следующий() Цикл
		НоваяСтрока = ТЗ.Добавить();
		НоваяСтрока.Покупатель = Остатки.Покупатель;
		НоваяСтрока.Выручка = Остатки.Выручка;
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ПолучитьВыручкуОтПокупателей2()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|		Обороты.КорСубконто1 КАК Покупатель,
	|		Обороты.СуммаОборотДт КАК Выручка
	|ИЗ
	|	РегистрБухгалтерии.ОсновнойРегистрБухгалтерии.Обороты(
	|											&НачалоПериода, 
	|											&КонецПериода, 
	|											, 
	|											Счет = &СчетКассы, 
	|											, 
	|											, 
	|											КорСчет = &СчетПокупателей, 
	|											) КАК Обороты";
		
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	Запрос.УстановитьПараметр("СчетКассы", ПланыСчетов.ОсновнойПланСчетов.Касса);
	Запрос.УстановитьПараметр("СчетПокупателей", ПланыСчетов.ОсновнойПланСчетов.Покупатели);
	
	Результат = Запрос.Выполнить();
   	Остатки = Результат.Выбрать();
	
	ТЗ.Очистить();
	Пока Остатки.Следующий() Цикл
		НоваяСтрока = ТЗ.Добавить();
		НоваяСтрока.Покупатель = Остатки.Покупатель;
		НоваяСтрока.Выручка = Остатки.Выручка;
	КонецЦикла;
	
	Элементы.ТЗ.Подвал = Истина;
	Элементы.ТЗВыручка.ГоризонтальноеПоложениеВПодвале = ГоризонтальноеПоложениеЭлемента.Право;
	Элементы.ТЗВыручка.ТекстПодвала = ТЗ.Итог("Выручка");

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьОстаток(Момент)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Остатки.СуммаОстаток
	|ИЗ
	|	РегистрБухгалтерии.ОсновнойРегистрБухгалтерии.Остатки(&Момент, Счет = &Счет, , ) КАК Остатки";

	Запрос.УстановитьПараметр("Момент", Момент);
	Запрос.УстановитьПараметр("Счет", ПланыСчетов.ОсновнойПланСчетов.Касса);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат 0;
	КонецЕсли; 
	
	Остатки = Результат.Выбрать();
	Остатки.Следующий();
	Возврат Остатки.СуммаОстаток;
	
КонецФункции

&НаКлиенте
Процедура ОстаткиНаДень(Команда)
	
	Остаток = 0;
	Момент = Дата(2010, 6, 1);
	Остаток = ПолучитьОстаток(Момент);

КонецПроцедуры

&НаКлиенте
Процедура ОстаткиНаКонецДня(Команда)
	
	Остаток = 0;
	Дата = Дата(2010, 6, 1);
	Момент = КонецДня(Дата);
	Остаток = ПолучитьОстаток(Момент);

КонецПроцедуры

&НаКлиенте
Процедура ОстаткиНаНачалоСледующегоДня(Команда)
	
	Остаток = 0;
	Дата = Дата(2010, 6, 1);
	ОдинДень = 60 * 60 * 24;
	Момент = НачалоДня(Дата + ОдинДень);
	Остаток = ПолучитьОстаток(Момент);

КонецПроцедуры

&НаКлиенте
Процедура ОстаткиНаГраницу(Команда)
	
	ПолучитьОстатокВключаяГраницу();

КонецПроцедуры

&НаСервере
Процедура ПолучитьОстатокВключаяГраницу()
	
	Остаток = 0;
	Дата = Дата(2010, 6, 1);
	КонДня = КонецДня(Дата);
	Граница = Новый Граница(КонДня, ВидГраницы.Включая);
    Момент = Граница.Значение;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Остатки.СуммаОстаток
	|ИЗ
	|	РегистрБухгалтерии.ОсновнойРегистрБухгалтерии.Остатки(&Момент, Счет = &Счет, , ) КАК Остатки";

	Запрос.УстановитьПараметр("Момент", Граница);
	Запрос.УстановитьПараметр("Счет", ПланыСчетов.ОсновнойПланСчетов.Касса);
	
	Результат = Запрос.Выполнить();
	Остатки = Результат.Выбрать();
	Остатки.Следующий();
	Остаток = Остатки.СуммаОстаток;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьОборот(НачПериода, КонПериода)
	
	Момент = Строка(НачПериода) + " - " + Строка(КонПериода);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Обороты.СуммаОборот
	|ИЗ
	|	РегистрБухгалтерии.ОсновнойРегистрБухгалтерии.Обороты(&НачПериода, &КонПериода, , Счет = &Счет, , , , ) КАК Обороты";

	Запрос.УстановитьПараметр("НачПериода", НачПериода);
	Запрос.УстановитьПараметр("КонПериода", КонПериода);
	Запрос.УстановитьПараметр("Счет", ПланыСчетов.ОсновнойПланСчетов.Касса);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат 0;
	КонецЕсли; 
	Обороты = Результат.Выбрать();
	Обороты.Следующий();
	Возврат Обороты.СуммаОборот;
	
КонецФункции

&НаСервере
Процедура ПолучитьОстатокИзОстаткиИОбороты(НачПериода, КонПериода)
	
	Момент = Строка(НачПериода) + " - " + Строка(КонПериода);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОстаткиИОбороты.СуммаНачальныйОстаток,
	|	ОстаткиИОбороты.СуммаОборот,
	|	ОстаткиИОбороты.СуммаКонечныйОстаток
	|ИЗ
	|	РегистрБухгалтерии.ОсновнойРегистрБухгалтерии.ОстаткиИОбороты(&НачПериода, &КонПериода, , , Счет = &Счет, , ) КАК ОстаткиИОбороты";
	
	Запрос.УстановитьПараметр("НачПериода", НачПериода);
	Запрос.УстановитьПараметр("КонПериода", КонПериода);
	Запрос.УстановитьПараметр("Счет", ПланыСчетов.ОсновнойПланСчетов.Касса);
	
	Результат = Запрос.Выполнить();
	ОстаткиИОбороты = Результат.Выбрать();
	ОстаткиИОбороты.Следующий();

	НачОстаток = ОстаткиИОбороты.СуммаНачальныйОстаток;
	Оборот1 = ОстаткиИОбороты.СуммаОборот;
	КонОстаток = ОстаткиИОбороты.СуммаКонечныйОстаток;
	
КонецПроцедуры

&НаКлиенте
Процедура ОборотыЗаДень(Команда)
	
	Оборот = 0;
	Дата = Дата(2010, 6, 1);
	НачПериода = НачалоДня(Дата);
	КонПериода = КонецДня(Дата);
	Оборот = ПолучитьОборот(НачПериода, КонПериода);

КонецПроцедуры

&НаКлиенте
Процедура ОстаткиИОборотыЗаДень(Команда)
	
	Дата = Дата(2010, 6, 1);
	НачПериода = НачалоДня(Дата);
	КонПериода = КонецДня(Дата);
	ПолучитьОстатокИзОстаткиИОбороты(НачПериода, КонПериода);
	
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда)
	
	ПолучитьРазвернутыеОстатки();
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьРазвернутыеОстатки()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Остатки.Счет КАК Счет,
	|	Остатки.Субконто1 КАК Контрагент,
	|	Остатки.СуммаОстатокДт КАК СвернутыйДт,
	|	Остатки.СуммаОстатокКт КАК СвернутыйКт,
	|	Остатки.СуммаРазвернутыйОстатокДт КАК РазвернутыйДт,
	|	Остатки.СуммаРазвернутыйОстатокКт КАК РазвернутыйКт
	|ИЗ
	|	РегистрБухгалтерии.ОсновнойРегистрБухгалтерии.Остатки(, Счет = &Счет, , ) КАК Остатки
	|ИТОГИ
	|	СУММА(СвернутыйДт),
	|	СУММА(СвернутыйКт),
	|	СУММА(РазвернутыйДт),
	|	СУММА(РазвернутыйКт)
	|	ПО
	|	Счет";
		
	Запрос.УстановитьПараметр("Счет", ПланыСчетов.ОсновнойПланСчетов.Контрагенты);
	
	Результат = Запрос.Выполнить();
   	Остатки = Результат.Выбрать();
	
	ТЗ2.Очистить();
	Пока Остатки.Следующий() Цикл
		НоваяСтрока = ТЗ2.Добавить();
		НоваяСтрока.Счет = Остатки.Счет;
		НоваяСтрока.Контрагент = Остатки.Контрагент;
		НоваяСтрока.СвернутыйДт = Остатки.СвернутыйДт;
		НоваяСтрока.СвернутыйКт = Остатки.СвернутыйКт;
		НоваяСтрока.РазвернутыйДт = Остатки.РазвернутыйДт;
		НоваяСтрока.РазвернутыйКт = Остатки.РазвернутыйКт;
	КонецЦикла;

КонецПроцедуры	

&НаКлиенте
Процедура ОстатокЗапросом(Команда)
	
	ПолучитьОстатокТовараЗапросом();
	
КонецПроцедуры

&НаКлиенте
Процедура ОстатокМетодом(Команда)
	
	ПолучитьОстатокТовараМетодом();
	
КонецПроцедуры

&НаКлиенте
Процедура ОстаткиТоваровЗапросом(Команда)
	
	ПолучитьОстаткиТоваровЗапросом();
	
КонецПроцедуры

&НаКлиенте
Процедура ОстаткиТоваровМетодом(Команда)
	
	ПолучитьОстаткиТоваровМетодом();
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьОстатокТовараЗапросом()
	
	МоментВремени = Новый Граница(КонецДня(Дата), ВидГраницы.Включая);
	СчетТоваров = ПланыСчетов.ОсновнойПланСчетов.Товары;

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Остатки.КоличествоОстатокДт
	|ИЗ
	|	РегистрБухгалтерии.ОсновнойРегистрБухгалтерии.Остатки(&МоментВремени, Счет = &Счет, , Субконто1 = &Субконто1 И Организация = &Организация) КАК Остатки";

	Запрос.УстановитьПараметр("МоментВремени", МоментВремени);
	Запрос.УстановитьПараметр("Счет", СчетТоваров);
	Запрос.УстановитьПараметр("Субконто1", Товар);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Результат = Запрос.Выполнить();
	Остатки = Результат.Выбрать();
	Остатки.Следующий();
	ОстатокТовара = Остатки.КоличествоОстатокДт;

КонецПроцедуры

&НаСервере
Процедура ПолучитьОстатокТовараМетодом()
	
	Момент = Новый Граница(КонецДня(Дата), ВидГраницы.Включая);
	СчетТоваров = ПланыСчетов.ОсновнойПланСчетов.Товары;
	
	Регистр = РегистрыБухгалтерии.ОсновнойРегистрБухгалтерии;
	Отбор = Новый Структура("Счет, Субконто1, Организация",	СчетТоваров, Товар,	Организация);

	ТаблицаРезультат = Регистр.Остатки(Момент, , Отбор, "" , "Количество");
	ОстатокТовара = ТаблицаРезультат.Итог("КоличествоОстатокДт");
	
КонецПроцедуры
 
&НаСервере
Процедура ПолучитьОстаткиТоваровЗапросом()
	
	Момент = Новый Граница(КонецДня(Дата), ВидГраницы.Включая);
	СчетТоваров = ПланыСчетов.ОсновнойПланСчетов.Товары;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Остатки.Субконто1,
	|	Остатки.Субконто2,
	|	Остатки.КоличествоОстатокДт,
	|	Остатки.КоличествоОстатокКт,
	|	Остатки.СуммаОстатокДт,
	|	Остатки.СуммаОстатокКт
	|ИЗ
	|	РегистрБухгалтерии.ОсновнойРегистрБухгалтерии.Остатки(&Момент, Счет = &Счет, , Организация = &Организация) КАК Остатки";
	
	Запрос.УстановитьПараметр("Момент", Момент);
	Запрос.УстановитьПараметр("Счет", СчетТоваров);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Результат = Запрос.Выполнить();
	ТаблицаРезультат = Результат.Выгрузить();
	
	ТЗ3.Очистить();
	Для Каждого СтрокаТаблицаРезультат Из ТаблицаРезультат Цикл
		НоваяСтрока = ТЗ3.Добавить();
		НоваяСтрока.Субконто1 = СтрокаТаблицаРезультат.Субконто1;
		НоваяСтрока.Субконто2 = СтрокаТаблицаРезультат.Субконто2;
		НоваяСтрока.КоличествоОстатокДт = СтрокаТаблицаРезультат.КоличествоОстатокДт;
		НоваяСтрока.КоличествоОстатокКт = СтрокаТаблицаРезультат.КоличествоОстатокКт;
		НоваяСтрока.СуммаОстатокДт = СтрокаТаблицаРезультат.СуммаОстатокДт;
		НоваяСтрока.СуммаОстатокКт = СтрокаТаблицаРезультат.СуммаОстатокКт;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьОстаткиТоваровМетодом()
	
	Момент = Новый Граница(КонецДня(Дата), ВидГраницы.Включая);
	СчетТоваров = ПланыСчетов.ОсновнойПланСчетов.Товары;
	
	Регистр = РегистрыБухгалтерии.ОсновнойРегистрБухгалтерии;
	Отбор = Новый Структура("Счет, Организация", СчетТоваров, Организация);
	ТаблицаРезультат = Регистр.Остатки(Момент, , Отбор, "Субконто1, Субконто2", "Количество, Сумма");
	
	ТЗ3.Очистить();
	Для Каждого СтрокаТаблицаРезультат Из ТаблицаРезультат Цикл
		НоваяСтрока = ТЗ3.Добавить();
		НоваяСтрока.Субконто1 = СтрокаТаблицаРезультат.Субконто1;
		НоваяСтрока.Субконто2 = СтрокаТаблицаРезультат.Субконто2;
		НоваяСтрока.КоличествоОстатокДт = СтрокаТаблицаРезультат.КоличествоОстатокДт;
		НоваяСтрока.КоличествоОстатокКт = СтрокаТаблицаРезультат.КоличествоОстатокКт;
		НоваяСтрока.СуммаОстатокДт = СтрокаТаблицаРезультат.СуммаОстатокДт;
		НоваяСтрока.СуммаОстатокКт = СтрокаТаблицаРезультат.СуммаОстатокКт;
	КонецЦикла;
			
КонецПроцедуры
