Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Проводки = Движения.ОсновнойРегистрБухгалтерии;
	
	// не новый документ, для нового считывать из базы не надо
	// не изменен набор записей, если изменен тоже не надо
	// набор еще не считывался из базы
	НужноСчитатьНабор = (НЕ Ссылка.Пустая() И НЕ Проводки.Модифицированность() И НЕ Проводки.Выбран());
	
	Если НужноСчитатьНабор Тогда  
		Проводки.Прочитать();
	КонецЕсли; 
	
	Для Каждого Проводка Из Движения.ОсновнойРегистрБухгалтерии Цикл
		Проводка.Период = Дата;
		Проводка.Организация = Организация;
	КонецЦикла; 
	
	КоличествоПроводок = Проводки.Количество();

	Если КоличествоПроводок > 0 Тогда
		ТекущаяАктивностьПроводок = Проводки[0].Активность;
		НужнаяАктивностьПроводок  = НЕ ПометкаУдаления;
		
		Если ТекущаяАктивностьПроводок <> НужнаяАктивностьПроводок Тогда
			Проводки.УстановитьАктивность(НужнаяАктивностьПроводок);
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры
